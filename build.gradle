plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.mindlog'
version = '0.0.1-SNAPSHOT'
description = 'Emotion Diary with Hotwire Native'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-webmvc'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'  // Thymeleaf 템플릿 엔진
	implementation 'org.jspecify:jspecify:1.0.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-security-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'io.github.cdimascio:dotenv-java:3.0.0'

	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

	// Source: https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation("com.fasterxml.jackson.core:jackson-databind:2.20.1")
	implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")

	// Spring Session Redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.session:spring-session-data-redis'

	// 로깅 관련 의존성SessionConfig
	implementation 'net.logstash.logback:logstash-logback-encoder:8.0'  // JSON 구조화 로깅
	implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.10.0'  // SQL 파라미터 바인딩 로깅
	implementation 'org.springframework:spring-aop'  // AOP (성능 로깅용)
	implementation 'org.aspectj:aspectjweaver'  // AspectJ weaver
}

tasks.named('test') {
	useJUnitPlatform()
}

// [수정] OS에 따른 npm 실행 파일 경로 설정
def isWindows = System.getProperty('os.name').toLowerCase().contains('windows')
def npmExecutable = isWindows ? 'npm.cmd' : 'npm'

// macOS/Linux에서 PATH에 npm이 잡히지 않는 경우를 대비한 절대 경로 체크
if (!isWindows) {
    def possiblePaths = ['/opt/homebrew/bin/npm', '/usr/local/bin/npm', '/usr/bin/npm']
    for (String path : possiblePaths) {
        if (file(path).exists()) {
            npmExecutable = path
            break
        }
    }
}

// [수정] 1. tasks.register를 사용하여 '게으른 등록(Lazy Registration)' 수행
tasks.register('npmInstall', Exec) {
    commandLine npmExecutable, 'install'
    onlyIf { !file('node_modules').exists() }
}

tasks.register('buildCss', Exec) {
    dependsOn 'npmInstall'
    commandLine npmExecutable, 'run', 'build-css'
}

tasks.register('copyPreline', Copy) {
    dependsOn 'buildCss' // CSS 빌드 후 복사 (순서 보장)
    from 'node_modules/preline/dist/preline.js'
    into 'src/main/resources/static/js'
}

tasks.register('copyFlatpickrJs', Copy) {
    dependsOn 'npmInstall'
    from 'node_modules/flatpickr/dist/flatpickr.min.js'
    from 'node_modules/flatpickr/dist/l10n/ko.js'
    into 'src/main/resources/static/js/libs'
}

tasks.register('copyFlatpickrCss', Copy) {
    dependsOn 'npmInstall'
    from 'node_modules/flatpickr/dist/flatpickr.min.css'
    into 'src/main/resources/static/css/libs'
}

// [수정] 2. 의존성 설정 (문자열로 태그 이름을 참조)
processResources.dependsOn 'copyPreline', 'copyFlatpickrJs', 'copyFlatpickrCss'
